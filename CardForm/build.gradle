apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 33
        versionName '3.0.5'

        consumerProguardFiles 'proguard.cfg'
    }

    testOptions {
        unitTests {
            all {
                jvmArgs '-noverify'
            }
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
		abortOnError false
    }

    resourcePrefix 'bt_'
}

dependencies {
    compile 'com.android.support:design:25.0.0'
    compile 'io.card:android-sdk:[5.5.0,6.0.0)'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.3'
    testCompile 'org.mockito:mockito-core:2.7.11'
}

/* maven release */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}
